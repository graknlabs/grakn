insert
#string, long, double, boolean

"entity2" sub entity, is-abstract
    has name;
"recommendable" sub entity2;
"place" sub entity2;
"organization" sub entity2;
"tag" sub recommendable;
"category" sub entity2;
"forum" sub entity2;

"subject-location" sub location;
"located-subject" sub role;
"resides" sub relationship, relates subject-location, relates located-subject;

# place
"continent" sub place;
"country" sub place;
"city" sub place;


"location" sub role;

"member-location" sub location;
"container-location" sub location;
"sublocate" sub relationship, relates member-location, relates container-location;
place plays member-location, plays container-location, plays subject-location;

# organization
"university" sub organization;
"company" sub organization;
organization plays located-subject;

# person
"person" sub entity2;

person
    has name,
    has firstname,
    has lastname,
    has gender,
    has birthday,
    has email,
    has age;

"name" sub attribute, datatype string;
"firstname" sub attribute, datatype string;
"lastname" sub attribute, datatype string;
"gender" sub attribute, datatype string;
"birthday" sub attribute, datatype string;
"email" sub attribute, datatype string;
"age" sub attribute, datatype long;

person plays located-subject;

"acquaintance" sub role;
"acquaintance1" sub acquaintance;
"acquaintance2" sub acquaintance;
"knows" sub relationship,
    relates acquaintance1,
    relates acquaintance2;
person plays acquaintance1, plays acquaintance2;

"student" sub role;
"enrolled-university" sub role;
"attends" sub relationship,
    relates student,
    relates enrolled-university;
person plays student;
university plays enrolled-university;

"employee" sub role;
"employer" sub role;
"employment" sub relationship, relates employee, relates employer;
person plays employee;
company plays employer;

"class-year" sub attribute, datatype string;
"attends-attribute-value" sub role;
"attends-attribute-owner" sub role;
"attends-attribute" sub relationship, relates attends-attribute-value, relates attends-attribute-owner;
attends plays attends-attribute-owner;
class-year plays attends-attribute-value;

"employment-startdate" sub attribute, datatype string;
"employment-attribute-value" sub role;
"employment-attribute-owner" sub role;
"employment-attribute" sub relationship, relates employment-attribute-value, relates employment-attribute-owner;
employment plays employment-attribute-owner;
employment-startdate plays employment-attribute-value;

# message
"message" sub entity2, is-abstract;

"message-attribute-owner" sub role;
"message-attribute-value" sub role;
"message-attribute" sub relationship, relates message-attribute-value, relates message-attribute-owner;
message plays message-attribute-owner;

"content" sub attribute, datatype string, plays message-attribute-value;
"length" sub attribute, datatype string, plays message-attribute-value;

"liked" sub role;
"liker" sub role;
"likes" sub relationship, relates liked, relates liker;
message plays liked;
person plays liker;

"written" sub role;
"writer" sub role;
"writes" sub relationship, relates written, relates writer;
message plays written;
person plays writer;

message plays located-subject;

"comment" sub message;

"reply-owner" sub role;
"reply-content" sub role;
"reply" sub relationship, relates reply-content, relates reply-owner;
comment plays reply-content;
message plays reply-owner;

"post" sub message;

"post-attribute-owner" sub role;
"post-attribute-value" sub role;
"post-attribute" sub relationship, relates post-attribute-owner, relates post-attribute-value;
post plays post-attribute-owner;

"image-file" sub attribute, datatype string, plays post-attribute-value;

# forum
"forum-attribute-owner" sub role;
"forum-attribute-value" sub role;
"forum-attribute" sub relationship, relates forum-attribute-value, relates forum-attribute-owner;
forum plays forum-attribute-owner;

"moderator" sub role;
"moderated" sub role;
"moderates" sub relationship, relates moderator, relates moderated;
person plays moderator;
forum plays moderated;

"forum-member" sub role;
"membered-forum" sub role;
"membership" sub relationship, relates forum-member, relates membered-forum;
person plays forum-member;
forum plays membered-forum;

"post-container" sub role;
"contained-post" sub role;
"containing" sub relationship, relates post-container, relates contained-post;
post plays contained-post;
forum plays post-container;

# tag
"subject-tag" sub role;
"tagged-subject" sub role;
"tagging" sub relationship, relates tagged-subject, relates subject-tag;
tag plays subject-tag;
forum plays tagged-subject;
message plays tagged-subject;
person plays tagged-subject;

# tag class
"group" sub role;
"subgroup" sub group;
"supergroup" sub group;
"subgrouping" sub relationship, relates subgroup, relates supergroup;
category plays subgroup, plays supergroup;

"grouped-tag" sub role;
"tag-group" sub role;
"grouping" sub relationship, relates grouped-tag, relates tag-group;
tag plays grouped-tag;
category plays tag-group;

#TODO: Kill this rubbish
#This should really be looked at and removed later on
#Filler relationship type to bypass role validation rule which states every role must be linked to at least on relationship type
#Before these roles were abstract but now role cannot be abstract. Hence the hack you see blow
"fake-relationship" sub relationship
    relates group
    relates acquaintance
    relates location;