define

currency sub attribute,
    value string;

country-name sub attribute,
    value string;

city-name sub attribute,
    value string;

transaction sub entity,
    owns currency,
    plays locates:located;

locates sub relation,
    relates location,
    relates located;

location-hierarchy sub relation,
    relates superior,
    relates subordinate;

location sub entity,
    abstract,
    plays location-hierarchy:superior,
    plays location-hierarchy:subordinate;

country sub location,
    owns country-name @key,
    owns currency,
    plays locates:location;

city sub location,
    owns city-name @key,
    plays locates:location;

rule locates-is-transitive:
when {
    $city isa city;
    $country isa country;
    $lh(superior: $country, subordinate: $city) isa location-hierarchy;
    $l1(located: $transaction, location: $city) isa locates;
} then {
    (located: $transaction, location: $country) isa locates;
};

rule transaction-currency-is-that-of-the-country:
when {
    $transaction isa transaction;
    $locates(located: $transaction, location: $country) isa locates;
    $country isa country, has currency $currency;
} then {
    $transaction has currency $currency;
};
