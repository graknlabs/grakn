insert

###################################################
####################Ontology######################
#################################################

"person" isa entity-type
      has-resource name;

"parent" isa role-type;
"child" isa role-type;
"Parent" isa relation-type, has-role parent, has-role child;
person plays-role parent, plays-role child;

"sibA" isa role-type;
"sibB" isa role-type;
"Sibling" isa relation-type, has-role sibA, has-role sibB;
person plays-role sibA, plays-role sibB;

"SG-role-A" isa role-type;
"SG-role-B" isa role-type;
"SameGen" isa relation-type, has-role SG-role-A, has-role SG-role-B;
person plays-role SG-role-A, plays-role SG-role-B;

"name" isa resource-type, datatype string;

####################################################
##################DATA#############################
####################################################

$ann isa person, has name "ann";
$bill isa person, has name "bill";
$john isa person, has name "john";
$peter isa person, has name "peter";

(parent: $john, child: $ann) isa Parent;
(parent: $john, child: $peter) isa Parent;
(parent: $john, child: $bill) isa Parent;

####################################################
##################RULES#############################
####################################################

isa inference-rule,
lhs {
($x, $y) isa Sibling;},
rhs {
($x, $y) isa SameGen;};

isa inference-rule,
lhs {
(parent: $x, child: $u) isa Parent;
($u, $v) isa SameGen;
(parent: $y, child: $v) isa Parent;},
rhs {
($x, $y) isa SameGen;};

isa inference-rule,
lhs {
(parent: $z, child: $x) isa Parent;
(parent: $z, child: $y) isa Parent;},
rhs {
($x, $y) isa Sibling;};