define

original-post sub relationship
  relates parent-message
  relates child-message;
parent-message sub role;
child-message sub role;
message plays child-message;
post plays parent-message;

forum-member sub relationship
  relates member-message
  relates group-forum;
member-message sub role;
group-forum sub role;
forum plays group-forum;
message plays member-message;

rootPost sub rule
when {
  $p isa post;
}
then {
  (parent-message: $p, child-message: $p) isa original-post;
};

childComments sub rule
when {
  (reply: $c, original: $m) isa reply-of;
  (child-message: $m, parent-message: $op) isa original-post;
}
then {
  (child-message: $c, parent-message: $op) isa original-post;
};

forumOfComments sub rule
when {
  (child-message: $m, parent-message: $op) isa original-post;
  (contained: $op, container: $forum) isa container-of;
}
then {
  (member-message: $m, group-forum: $forum) isa forum-member;
};