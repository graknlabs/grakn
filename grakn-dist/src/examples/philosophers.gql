insert


# Concept types

person sub entity
    plays-role philosopher
    plays-role teacher
    plays-role student
    plays-role thinker
    has-resource name
    has-resource title
    has-resource epithet;

school sub entity
    has-resource name
    plays-role philosophy;

fact sub entity
    plays-role thought
    has-resource name
    has-resource description;

name sub resource datatype string;
title sub resource datatype string;
epithet sub resource datatype string;
description sub resource datatype string;


# Relation types

practice sub relation
    has-role philosopher
    has-role philosophy;
philosopher sub role;
philosophy sub role;

education sub relation
    has-role teacher
    has-role student;
teacher sub role;
student sub role;

knowledge sub relation
    has-role thinker
    has-role thought
    plays-role thought;
thinker sub role;
thought sub role;


# Instances

$socrates isa person, has name "Socrates";
$plato isa person, has name "Plato";
$aristotle isa person, has name "Aristotle";

$alexander isa person
    has name "Alexander"
    has epithet "The Great"
    has title "Hegeon"
    has title "King of Macedon"
    has title "Shah of Persia"
    has title "Pharaoh of Egypt"
    has title "Lord of Asia";

$peripateticism isa school, has name "Peripateticism";
$platonism isa school, has name "Platonism";
$idealism isa school, has name "Idealism";
$cynicism isa school, has name "Cynicism";

$sun-fact isa fact, has name 'sun-fact', has description "The Sun is bigger than the Earth";
$cave-fact isa fact, has name 'cave-fact', has description "Caves are mostly pretty dark";
$nothing isa fact, has name 'nothing';


# Relations

(philosopher: $socrates, philosophy: $platonism) isa practice;
(philosopher: $plato, philosophy: $idealism) isa practice;
(philosopher: $plato, philosophy: $platonism) isa practice;
(philosopher: $aristotle, philosophy: $peripateticism) isa practice;

(teacher: $socrates, student: $plato) isa education;
(teacher: $plato, student: $aristotle) isa education;
(teacher: $aristotle, student: $alexander) isa education;

(thinker: $aristotle, thought: $sun-fact) isa knowledge;
(thinker: $plato, thought: $cave-fact) isa knowledge;

$socratesKnowsNothing (thinker: $socrates, thought: $nothing) isa knowledge;
(thinker: $socrates, thought: $socratesKnowsNothing) isa knowledge;
