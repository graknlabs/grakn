insert


###################################################
####################Ontology######################
#################################################

"Person" isa entity-type
    has-resource name;

"friendA" isa role-type;
"friendB" isa role-type;
"Friend" isa relation-type, has-role friendA, has-role friendB;
Person plays-role friendA, plays-role friendB;

"parent" isa role-type;
"child" isa role-type;
"Parent" isa relation-type, has-role parent, has-role child;
Person plays-role parent, plays-role child;

"person" isa role-type;
"ancestor-friend" isa role-type;
"Ancestor-friend" isa relation-type, has-role person, has-role ancestor-friend;
Person plays-role person, plays-role ancestor-friend;

"name" isa resource-type, datatype string;

####################################################
##################DATA#############################
####################################################

$a isa Person, has name "a";
$b isa Person, has name "b";
$c isa Person, has name "c";
$d  isa Person, has name "d";
$g  isa Person, has name "g";

(parent: $a, child: $b) isa Parent;
(parent: $b, child: $c) isa Parent;
(friendA: $a, friendB: $g) isa Friend;
(friendA: $c, friendB: $d) isa Friend;

####################################################
##################RULES#############################
####################################################

isa inference-rule,
lhs {
($x, $y) isa Friend;},
rhs {
($x, $y) isa Ancestor-friend;};

isa inference-rule,
lhs {
(parent: $x, child: $z) isa Parent;
(person: $z, ancestor-friend: $y) isa Ancestor-friend;},
rhs {
(person: $x, ancestor-friend: $y) isa Ancestor-friend;};
